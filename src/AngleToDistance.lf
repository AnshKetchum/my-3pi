/**
 * Display encoder measurements from the wheels of the
 * <a href="https://www.pololu.com/docs/0J86">Pololu 3pi+ 2040 robot</a>.
 * @author Abhi Gundrala
 * @author Edward A. Lee
 */
target C {
  platform: {
    name: "rp2040",
    board: "pololu_3pi_2040_robot"
  },
  single-threaded: true
}

import Display from "lib/Display.lf"
import Encoders from "lib/Encoders.lf"

main reactor {
  display = new Display()
  encoder = new Encoders()

  timer t(0, 1 s)

  reaction(t) -> encoder.trigger {=
    lf_set(encoder.trigger, true);
  =}

  reaction(startup) -> display.line0 {=
    lf_set(display.line0, "ENCODERS:");
  =}

  reaction(encoder.left) -> display.line1 {=
    static char buf[17];

    // s = r * theta ==> r * pi / 180 * degree
    snprintf(buf, 17, "L: %f cm", ((float) encoder.left->value) * 3.175/2 * 0.0174533);
    lf_set(display.line1, buf);
  =}

  reaction(encoder.right) -> display.line2 {=
    static char buf[17];
    snprintf(buf, 17, "R: %f cm", ((float) encoder.right->value) * 3.175/2 * 0.0174533);
    lf_set(display.line2, buf);
  =}
}
